!* -*-Teco-*-!
!~Filename~:! !Patches to EMACS macros!
PATCH

!Select Buffer:! !C Select or create buffer, given its name.
If called from a ^R character, read the buffer name from the terminal
or should be given a buffer number as a numeric arg.
As a subroutine, needs a buffer number (or buffer name pointer) as a
prefix arg, or a buffer name as a string arg.
If there is a buffer with that name, it is selected.
Otherwise, a buffer with that name is created and selected.
When a new buffer is selected the first time, if QBuffer Creation Hook
is nonzero, it is run after the buffer is selected.!

    MMM_&_Check_Top_Levelbuffers
    [..h [4 0[3			    !* Make sure we can display the bfr we switch to.!
    FF&1"N U3'		    !* Numeric arg => use it as buffer or buffer # to select.!
    "# "E :F"G :i3''	    !* No postcomma arg or precomma arg => read string arg.!
      Q3"E M.M List_BuffersF[HELPMAC	    !* Else must read from tty.!
         "N u4'		    !* Precomma arg is prompt string to use.!
           "# :i4 Select_Buffer'
	 QPrevious_Buffer[3	    !* Get name of default new buffer to put in prompt.!
	 1,Q3M(M.M &_Find_Buffer)"L Q:.B(1)U3'
	 3,m(m.m &_Read_Line)4_(3):_u3''	    !* Read name of buffer to select.!
    Q3[5			    !* Save name (or buffer #) in Q5.!
    FQ3"E QPrevious_BufferU3'	    !* Null string means previous buffer.!
    1,Q3 M(M.M&_Find_Buffer)[1	    !* Get index in buffer table of this name or number.!
    Q1u4			    !* Q4 remains negative, for a new buffer.!
    Q1"L			    !* No such buffer => make one now.!
      FQ3"L :I*No_such_buffer FS ERR'	    !* Refuse to create buffer if bfr number spec'd.!
      FQ5"E 0U1 0U4'		            !* If ^XB<cr> and prev bfr non ex, use 1st buffer instead.!
      "# Q3 M(M.M &_Create_Buffer)U1''   !* Else create the specified buffer.!
    QBuffer_IndexU3
    Q1-Q3"E 0'
    1F[Noquit			    !* Prevent quitting half-switched.!
    0FO..Q Buffer_Deselection_Hook[5
    Q5"N M5'
    Q.B[..O ZJ 0K ]..O	    !* Get gap in buffer table out of the way.!
    Q3,9F.B			    !* Swap old buffer's locals back into its entry.!
    QBuffer_FilenamesU:.B(Q3+2)   !* Stick its filenames back into entry.!
    QMode U:.B(Q3+3)		    !* Stick selected mode into entry.!

!* Simultaneously swap out old buffer's TECO default filenames
 and window address, and swap in the new buffer's.!
    Q:.B(Q1+6) FS WINDOW U:.B(Q3+6)
    Q:.B(Q1+5)F"E W' FS D FILE U:.B(Q3+5)

!* We now are "between buffers".!
    Q:.B(Q1+2) UBuffer_Filenames  !* Get filenames of new buffer.!
    Q:.B(Q1+1)UBuffer_Name	    !* For our records, save its name!
    Q:.B(Q1+3)UMode
    Q1 UBuffer_Index		    !* and its index, for when we deselect it.!
    Q:.B(Q3+1) UPrevious_Buffer   !* remember previously selected buffer's name.!
    Q:.B(Q1+3) U3
    Q1,9F.B			    !* Get new buffer's local variable values.!
    1FSMODE CHANGE		    !* Recompute mode line eventually.!
    Q:.B(Q1+4) U..O		    !* Now really switch to this buffer.!
    0FO..Q Buffer_Selection_HookF"N [..N'
    Q:.B(Q1+5)"N '		    !* If buffer has been selected before, that's all.!
    Q:.B(Q1+1)U4
    FS OS TECO "E F64'"#Q4' FS DFN1    !* If buffer selected for 1st time, set default fn1 from name.!
    0FO..QBuffer_Creation_HookU4   !* If buffer selecetd for 1st time, maybe run user's hook.!
    Q4"N M4'
    
