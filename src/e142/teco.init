!* -*-TECO-*- *!

!* This is the TECO INIT for EMACS.  It sets up the EMACS environment
   necessary for dumping a new EMACS EJ file. *!
ft entering teco.init 
 -1fs^Idisablew		    !* do this first for comments!
 :ejDSK:EMACS;EMACS :EJu0	    !* load in the EMACS library!
 er ec fs i fileu1 fs if versionu2	    !* save filename and version of!
1u4					    !* EMACS library!
 fs osteco"n			    !* Twenex!
    :ejDSK:<EMACS>TWENEX.:EJw    !* load in the TWENEX library!
    er ec fs i fileu3		    !* save filename of TWENEX library!
    '
 :i*& Macro Get,q0m(q0+4)u.m	    !* call the loader in the EMACS library!
				    !* to find .M!
 f[ :ej page			    !* save :EJ page for flushing EINIT!
 :ejDSK:EMACS;EINIT :EJw	    !* load EINIT library!
2u4
 m(m.m& Load Essential Environment)
3u4
 q1m.vEMACS Library Filename	    !* save EMACS library filename!
 q2m.vEMACS Versionw		    !* version no. too!
 fs osteco"n			    !* Twenex!
   q3m.vTWENEX Library Filenamew'  !* save TWENEX library filename!
4u4
 m(m.m& Load Patches)		    !* Load fixes written since [PURE] was made!
5u4
 m.vMM ^R Exit to EXEC
 m(m.m& Load Default Environment)
6u4
 m(m.mPurify Variables)	    !* smash variable names to point to pure!
				    !* space if there is a copy there!
 f] :ej page			    !* flush EINIT!
7u4
!* Now create a Startup to be called from the ..L!
 fsosteco"e @:i*|		    !* ITS!
	etDSK:EMACS		    !* set device name and FN2!
	fs hsname fs dsnamew	    !* fill in directory!
	fs xuname fs dfn1w	    !* and FN1!
	1:<er>"l
	  1:<er*>"l
	    erEMACS;*''
	fs d file[2 et@ >
   !** end of system-dependent code!
        fs msname fs dsname
	qEMACS Version:\u1	    !* sigh, still need this!
	@y 1a-127"n ]2 :m(.,zfx*)'  !* if init doesnt begin with rubout then!
				    !* execute it as TECO commands!
	hk 1,m(m.mLoad Library)2	    !* else load it as a library,!
				    !* saving the filename!
	]2 :m..l
    |m.vMM & Startup EMACS'
 "# @:i*|			    !* Twenex!
	etDSK:EMACS.INIT fs hsname fs dsname
	1:<er>"l erDSK:<EMACS>'
	fs d file[2 et foo..
   !** end of system-dependent code!
	fs msname fs dsname
	qEMACS Version:\u1	    !* sigh, still need this!
	@y 1a-127"n ]2 :m(.,zfx*)'  !* if init doesnt begin with rubout then!
				    !* execute it as TECO commands!
	hk 1,m(m.mLoad Library)2	    !* else load it as a library!
				    !* saving the filename!
	]2 :m..l
    |m.vMM & Startup EMACS'
0u4
 0u0 0u1 0u2 0u3		    !* zero the q-regs we've used!

 hk
